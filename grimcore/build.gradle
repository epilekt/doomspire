plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
}

group   = findProperty('core_mod_group_id') ?: 'com.doomspire'
version = findProperty('core_mod_version')  ?: '0.1.0'

base {
    archivesName = (findProperty('core_mod_id') ?: 'grimcore')
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://maven.neoforged.net/releases' }
}

dependencies {
    // grimcore — библиотека, компилируемся против NeoForge API
    def neo = findProperty('neo_version') ?: '21.1.208'
    compileOnly "net.neoforged:neoforge:${neo}"
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
            artifactId = base.archivesName.get()
        }
    }
    repositories {
        maven { url = uri("file://${project.projectDir}/repo") }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}



